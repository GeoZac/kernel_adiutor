buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId 'com.kerneladiutor.mod'
        minSdkVersion 25
        targetSdkVersion 27
        versionCode 15
        versionName "1.0 build " + getDate()
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            signingConfig signingConfigs.debug
            resValue "string", "APP_UPDATE_URL", "https://raw.githubusercontent.com/yoinx/kernel_adiutor/master/download/app/app-debug.json"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "APP_UPDATE_URL", "https://raw.githubusercontent.com/yoinx/kernel_adiutor/master/download/app/app-release.json"
        }
    }

}

static def getDate() {
    def date = new Date()
    def formattedDate = date.format('MM-dd-yyyy')
    return formattedDate
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':library')
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation ('io.karim:materialtabs:2.0.3'){
        exclude group: 'com.android.support'
        exclude module: 'appcompat-v4'
    }
    implementation ('net.i2p.android.ext:floatingactionbutton:1.10.1'){
        exclude group: 'com.android.support'
        exclude module: 'appcompat-v4'
    }
    implementation 'org.cyanogenmod:platform.sdk:1.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'com.android.support:multidex:1.0.2'
    implementation('com.crashlytics.sdk.android:crashlytics:2.5.6@aar') {
        transitive = true
    }
}
